group 'org.biais'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.51'
    ext.ktor_version = '0.9.0'
    ext.kotlinx_html_version = '0.6.4'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'io.ktor.server.netty.DevelopmentEngine'

configurations {
    ktlint
}

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "https://dl.bintray.com/kotlin/exposed" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    // Ktor
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-client-core:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"

    // Logging
    compile "ch.qos.logback:logback-classic:1.2.1"

    // Kotlinx.html
    compile "org.jetbrains.kotlinx:kotlinx-html-jvm:$kotlinx_html_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"

    // Fuel (HTTP library)
    compile 'com.github.kittinunf.fuel:fuel:1.11.0'

    // Klaxon (JSON parser)
    compile 'com.beust:klaxon:0.30'

    // Exposed - SQL library
    compile 'org.jetbrains.exposed:exposed:0.8.7'
    compile 'org.xerial:sqlite-jdbc:3.20.0'

    // Ktlint
    ktlint "com.github.shyiko:ktlint:0.11.1"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
